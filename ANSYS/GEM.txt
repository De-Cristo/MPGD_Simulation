! A sample script to generate the triple GEM model in Ansys
! Author : Licheng Zhang (PKU)
! The comments in Ansys start with a !         
! First let us start from clearing the data base  
/CLEAR,START
! Give a name to the job for analysis and 1 because we want to create new error,log,lock... files all having same name (gem). 
/FILNAME,gem,1
! Define a main title
/TITLE,singlegem
! This is a preprocessor in Ansys which is called PREP-7 (general input data preprocessor)
/PREP7
! This command will activate the p-method (off because Garfield does not have shape functions for the p-method and 1 because we want to solve electrostatic problems) 
/PMETH,OFF,1
! KEYW - Used by the GUI specific for changing its behaviour and the keywords which follow it are used to specify that we are solving electric problems within 
! electromagnetic domain
KEYW,PR_ELMAG,1
KEYW,MAGELC,1
! Define a 3-D 10-node tetrahedral electrostatic solid (solid 123) using ET command
ET,1,SOLID123
! Define materials based on permittivity (PERX) and resistivity (RSVX) and then number it  
MP,PERX,1,1e10
MP,RSVX,1,0 !METAL
MP,PERX,2,3.5 !KAPTON
MP,PERX,3,1 !GAS
! Let us build the model now.
! GEM
! We use three types of solid volumes to build the GEM: 
! block, cone and cylinder. 
! First we build three blocks: one 50 micron high block sandwiched between the two 5 micron thick blocks.
BLOCK,0,140,0,140,0,50 !1
BLOCK,0,140,0,140,50,55 !2
BLOCK,0,140,0,140,0,-5 !3
! Next we build two inverted cones. Later these cones and cylinders are subtracted to give the biconical hole structure in the GEM. 
CONE,25,35,25,50,0,360 !4
CONE,35,25,0,25,0,360 !5
! And the two cylinders.
CYLIND,35,,50,55,0,360 !6
CYLIND,35,,0,-5,0,360 !7
! WPOFFS (Working Plane OFFset) moves the current working plane, in this case 70 microns both in positive x and y direction. 
WPOFFS,70,70,0
CONE,25,35,25,50,0,360 !8
CONE,35,25,0,25,0,360 !9
CYLIND,35,,50,55,0,360 !10
CYLIND,35,,0,-5,0,360 !11
WPOFFS,-70,-70,0
WPOFFS,0,140,0
CONE,25,35,25,50,0,360 !12
CONE,35,25,0,25,0,360 !13
CYLIND,35,,50,55,0,360 !14
CYLIND,35,,0,-5,0,360 !15
WPOFFS,0,-140,0
WPOFFS,140,0,0
CONE,25,35,25,50,0,360 !16
CONE,35,25,0,25,0,360 !17
CYLIND,35,,50,55,0,360 !18
CYLIND,35,,0,-5,0,360 !19
WPOFFS,-140,0,0
WPOFFS,140,140,0
CONE,25,35,25,50,0,360 !20
CONE,35,25,0,25,0,360 !21
CYLIND,35,,50,55,0,360 !22
CYLIND,35,,0,-5,0,360 !23
WPOFFS,-140,-140,0 ! 0,0,0
! After building a total of 20 cones and cylinders 
! (two cones and cylinders each at the corners and two at the center) 
! we subtract them from the three blocks using the VSBV command. 
! Note the numbers given to the solids.
VSBV,1,4,,DELETE,DELETE ! delete 1 and 4, the rest became 24
VSBV,24,5,,DELETE,DELETE! delete 24 and 5, the rest became 1
VSBV,1,8,,DELETE,DELETE ! delete 1 and 8, the rest became 4
VSBV,4,9,,DELETE,DELETE
VSBV,1,12,,DELETE,DELETE
VSBV,4,13,,DELETE,DELETE
VSBV,1,16,,DELETE,DELETE
VSBV,4,17,,DELETE,DELETE
VSBV,1,20,,DELETE,DELETE
VSBV,4,21,,DELETE,DELETE
VSBV,2,6,,DELETE,DELETE
VSBV,4,10,,DELETE,DELETE
VSBV,2,14,,DELETE,DELETE
VSBV,4,18,,DELETE,DELETE
VSBV,2,22,,DELETE,DELETE
VSBV,3,7,,DELETE,DELETE
VSBV,2,11,,DELETE,DELETE
VSBV,3,15,,DELETE,DELETE
VSBV,2,19,,DELETE,DELETE
VSBV,3,23,,DELETE,DELETE
! Cathode
WPOFFS,0,0,2055 ! 2mm drift
BLOCK,0,140,0,140,0,5 ! 3
WPOFFS,0,0,-2055
! Anode
WPOFFS,0,0,-1005
BLOCK,0,140,0,140,0,-5 ! 5
WPOFFS,0,0,1005
! Gas
BLOCK,0,140,0,140,2055,-1005 !6
VSBV,6,1,,,KEEP
VSBV,7,2,,,KEEP
VSBV,6,3,,,KEEP
VSBV,7,4,,,KEEP
VSBV,6,5,,,KEEP
VGLUE,ALL
! Set the material properties

